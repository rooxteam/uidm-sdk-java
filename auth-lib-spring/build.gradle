buildscript {
    repositories {
        if (project.hasProperty('rooxExternalAccess')) {
            mavenLocal()
            mavenCentral()
            maven { url 'https://repo.springsource.org/plugins-release/' }
        } else {
            maven { url 'https://nexus.rooxintra.net/content/repositories/releases' }
            maven { url 'https://nexus.rooxintra.net/content/repositories/public' }
        }
    }
    dependencies {
        classpath("com.rooxteam.corporate:gradle-plugins:$rooxGradlePlugins")
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}


apply plugin: 'roox-doc'
apply plugin: 'roox-jar'
apply plugin: 'com.bmuschko.nexus'

// give test dependencies access to compileOnly dependencies to emulate providedCompile
configurations {
    testImplementation.extendsFrom compileOnly
}

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

dependencies {

    compile project(":auth-lib-common")

    // for lombok magic
    compileOnly("org.projectlombok:lombok:${lombokVersion}")

    // for compiling upon spring security
    compileOnly("org.springframework.security:spring-security-core:$springSecurityVersion")
    compileOnly("org.springframework.security:spring-security-web:$springSecurityVersion")
    compileOnly("org.springframework.security:spring-security-config:$springSecurityVersion")
    compileOnly("org.springframework:spring-webmvc:$springVersion")

    // for compiling upon servlet api
    compileOnly("javax.servlet:servlet-api:${servletVersion}")

    // for typed logging
    compile("org.jboss.logging:jboss-logging:$jbossLoggingVersion")
    compileOnly("org.jboss.logging:jboss-logging-processor:$jbossLoggingExtensionsVersion")
    compileOnly("org.jboss.logging:jboss-logging-annotations:$jbossLoggingExtensionsVersion")

    // for testing
    testCompile("junit:junit:$junitVersion")
    testCompile("org.mockito:mockito-all:$mockitoVersion")
    testCompile('javax.servlet:javax.servlet-api:4.0.1')
    testCompile("org.springframework:spring-test:${springVersion}")
}

javadoc {
    options.charSet = 'UTF-8'
    options.addStringOption('Xdoclint:none', '-quiet')
}

modifyPom {
    project {
        name 'auth-lib-spring'
        description 'uidm-sdk-java'
        groupId 'com.rooxteam.uidm.sdk'
        artifactId 'auth-lib-spring'
        url 'https://bitbucket.org/rooxteam/uidm-sdk-java'
        packaging 'jar'

        scm {
            url 'https://bitbucket.org/rooxteam/uidm-sdk-java'
            connection 'scm:https://bitbucket.org/rooxteam/uidm-sdk-java.git'
            developerConnection 'scm:git://bitbucket.org/rooxteam/uidm-sdk-java.git'
        }

        licenses {
            license {
                name 'CDDL-1.1'
                url 'https://javaee.github.io/glassfish/LICENSE'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'roox'
                name 'RooX Solutions'
                email 'support@roox.ru'
            }
        }
    }
}

extraArchive {
    sources = true
    tests = false
    javadoc = false
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}
