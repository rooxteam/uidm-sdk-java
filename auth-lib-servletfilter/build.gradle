buildscript {
    repositories {
        if (project.hasProperty('rooxExternalAccess')) {
            mavenLocal()
            mavenCentral()
        } else {
            maven { url 'http://nexus.rooxintra.net/content/repositories/releases' }
            maven { url 'http://nexus.rooxintra.net/content/repositories/public' }
        }
    }
    dependencies {
        classpath("com.rooxteam.corporate:gradle-plugins:$rooxGradlePlugins")
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

plugins {
    // for configuration see gradle/jcenter.gradle
    id "com.jfrog.bintray" version "1.8.4"
}


apply plugin: 'roox-doc'
apply plugin: 'roox-jar'
apply plugin: 'com.bmuschko.nexus'
apply from: 'gradle/jcenter.gradle'

// give test dependencies access to compileOnly dependencies to emulate providedCompile
configurations {
    testImplementation.extendsFrom compileOnly
}

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

dependencies {

    compile project(":auth-lib-common")

    compile("org.jboss.logging:jboss-logging:$jbossLoggingVersion")
    compileOnly("org.jboss.logging:jboss-logging-processor:$jbossLoggingExtensionsVersion")
    compileOnly("org.jboss.logging:jboss-logging-annotations:$jbossLoggingExtensionsVersion")

    // for compiling upon servlet api
    compileOnly("javax.servlet:servlet-api:${servletVersion}")

    // for testing
    testCompile("junit:junit:$junitVersion")
    testCompile("org.mockito:mockito-all:$mockitoVersion")
    testCompile('javax.servlet:servlet-api:2.5')
    testCompile("org.jboss.logging:jboss-logging:$jbossLoggingVersion")
}

javadoc {
    options.charSet = 'UTF-8'
    options.addStringOption('Xdoclint:none', '-quiet')
}

modifyPom {
    project {
        name 'auth-lib-servletfilter'
        description 'uidm-sdk-java'
        groupId 'com.rooxteam.uidm.sdk'
        artifactId 'auth-lib-servletfilter'
        url 'https://bitbucket.org/rooxteam/uidm-sdk-java'
        packaging 'jar'

        scm {
            url 'https://bitbucket.org/rooxteam/uidm-sdk-java'
            connection 'scm:https://bitbucket.org/rooxteam/uidm-sdk-java.git'
            developerConnection 'scm:git://bitbucket.org/rooxteam/uidm-sdk-java.git'
        }

        licenses {
            license {
                name 'CDDL-1.1'
                url 'https://javaee.github.io/glassfish/LICENSE'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'roox'
                name 'RooX Solutions'
                email 'support@roox.ru'
            }
        }
    }
}

extraArchive {
    sources = false
    tests = false
    javadoc = false
}

nexus {
    sign = true
    repositoryUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    snapshotRepositoryUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
}
