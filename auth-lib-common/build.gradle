buildscript {
    repositories {
        if (project.hasProperty('rooxExternalAccess')) {
            mavenLocal()
            mavenCentral()
            maven { url 'http://repo.springsource.org/plugins-release/' }
        } else {
            maven { url 'http://nexus.rooxintra.net/content/repositories/releases' }
            maven { url 'http://nexus.rooxintra.net/content/repositories/public' }
        }
    }
    dependencies {
        classpath("com.rooxteam.corporate:gradle-plugins:$rooxGradlePlugins")
    }
}

plugins {
    // for configuration see gradle/jcenter.gradle
    id "com.jfrog.bintray" version "1.8.4"
}


apply plugin: 'roox-doc'
apply plugin: 'roox-jar'
apply from: 'gradle/jcenter.gradle'

// give test dependencies access to compileOnly dependencies to emulate providedCompile
configurations {
    testImplementation.extendsFrom compileOnly
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {

    // TODO: for HttpStatus, needs fixing
    compileOnly(group: 'org.springframework', name: 'spring-web', version: springVersion)

    // for com.rooxteam.sso.aal.client.cookie.store.enablePerRequest, ok until UIDM needs sticky session
    compile("com.rooxteam.sso:request-cookie-store:$requestCookieStoreVersion")

    // for backward compat, ok to have it provided
    compileOnly("commons-configuration:commons-configuration:$apacheConfigurationVersion")

    // for RooxconfigConfiguration, ok to have it provided
    compileOnly("com.rooxteam.commons:rooxconfig:$rooxconfigVersion")

    // for HttpClient, ok for now
    compile("org.apache.httpcomponents:httpclient:$apacheHttpClientVersion")

    compileOnly("ch.qos.logback:logback-classic:$logbackVersion")

    // for typed logging, ok for now
    compile("org.jboss.logging:jboss-logging:$jbossLoggingVersion")
    compileOnly("org.jboss.logging:jboss-logging-processor:$jbossLoggingExtensionsVersion")
    compileOnly("org.jboss.logging:jboss-logging-annotations:$jbossLoggingExtensionsVersion")

    // for cache, ok for now
    compile("com.google.guava:guava:$guavaVersion")

    compileOnly("io.micrometer:micrometer-core:$micrometerVersion")

    // for json processing, ok for now
    compile("com.fasterxml.jackson.core:jackson-core:${jacksonVersion}")
    compile("com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}")

    // for JWT, ok
    compile("com.nimbusds:nimbus-jose-jwt:${nimbusJoseJwtVersion}")

    // for compiling upon servlet api
    compileOnly("javax.servlet:servlet-api:${servletVersion}")

    // for lombok magic
	compileOnly("org.projectlombok:lombok:${lombokVersion}")

    // for testing
    testCompile("junit:junit:$junitVersion")
    testCompile("org.mockito:mockito-all:$mockitoVersion")
    testCompile('javax.servlet:servlet-api:2.5')
}

javadoc {
    options.charSet = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}
